cmake_minimum_required(VERSION 3.26)

set(streambuf-filters_known_comps static shared)
set(streambuf-filters_comp_static NO)
set(streambuf-filters_comp_shared NO)
foreach (streambuf-filters_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (streambuf-filters_comp IN_LIST streambuf-filters_known_comps)
        set(streambuf-filters_comp_${streambuf-filters_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "streambuf-filters does not recognize component `${streambuf-filters_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (streambuf-filters_comp_static AND streambuf-filters_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "streambuf-filters `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(streambuf-filters_static_targets "${CMAKE_CURRENT_LIST_DIR}/streambuf-filters-static-targets.cmake")
set(streambuf-filters_shared_targets "${CMAKE_CURRENT_LIST_DIR}/streambuf-filters-shared-targets.cmake")

macro(streambuf-filters_load_targets type)
    if (NOT EXISTS "${streambuf-filters_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "streambuf-filters `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${streambuf-filters_${type}_targets}")
endmacro()

if (streambuf-filters_comp_static)
    streambuf-filters_load_targets(static)
elseif (streambuf-filters_comp_shared)
    streambuf-filters_load_targets(shared)
elseif (DEFINED streambuf-filters_SHARED_LIBS AND streambuf-filters_SHARED_LIBS)
    streambuf-filters_load_targets(shared)
elseif (DEFINED streambuf-filters_SHARED_LIBS AND NOT streambuf-filters_SHARED_LIBS)
    streambuf-filters_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${streambuf-filters_shared_targets}")
        streambuf-filters_load_targets(shared)
    else ()
        streambuf-filters_load_targets(static)
    endif ()
else ()
    if (EXISTS "${streambuf-filters_static_targets}")
        streambuf-filters_load_targets(static)
    else ()
        streambuf-filters_load_targets(shared)
    endif ()
endif ()
